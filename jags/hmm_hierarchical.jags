model {
  # prior
  # hierarchical of all parameters by provice

  # overall means
  gamma_group ~ dnorm(0, 1)
  phi_group ~ dnorm(0, 1)
  p_group ~ dnorm(0, 1)

  # super annoying parameterization bullshit
  gamma_sigma_group ~ dt(0, 1, 1) T(0, )
  phi_sigma_group ~ dt(0, 1, 1) T(0, )
  p_sigma_group ~ dt(0, 1, 1) T(0, )
  gamma_tau_group <- pow(gamma_sigma_group, -2)
  phi_tau_group <- pow(gamma_sigma_group, -2)
  p_tau_group <- pow(gamma_sigma_group, -2)

  for(j in 1:nprov) {
    psi[j] ~ dunif(0, 1)  # initial state probability
  }
  
  # swap nprov and nindiv and make it clearer
  p_sigma[nyear] ~ dt(0, 1, 1) T(0, )
  p_mu[nyear] ~ dnorm(p_group, p_tau_group)
  p_tau[nyear] <- pow(p_sigma[nyear], -2)
  for(j in 1:nprov) {
    p_norm[nyear, j] ~ dnorm(p_mu[nyear], p_tau[nyear])
    p[nyear, j] <- ilogit(p_norm[nyear, j])
  }
  for(i in 1:(nyear - 1)) {
    # temporal mean
    gamma_mu[i] ~ dnorm(gamma_group, gamma_tau_group)
    phi_mu[i] ~ dnorm(phi_group, phi_tau_group)
    p_mu[i] ~ dnorm(p_group, p_tau_group)

    # super annoying parameterization bullshit
    gamma_sigma[i] ~ dt(0, 1, 1) T(0, )
    phi_sigma[i] ~ dt(0, 1, 1) T(0, )
    p_sigma[i] ~ dt(0, 1, 1) T(0, )
    gamma_tau[i] <- pow(gamma_sigma[i], -2)
    phi_tau[i] <- pow(phi_sigma[i], -2)
    p_tau[i] <- pow(p_sigma[i], -2)
 
    # given whatever year we are on, go through all the provinces
    for(j in 1:nprov) {
      gamma_norm[i, j] ~ dnorm(gamma_mu[i], gamma_tau[i])
      phi_norm[i, j] ~ dnorm(phi_mu[i], phi_tau[i])
      p_norm[i, j] ~ dnorm(p_mu[i], p_tau[i])
    }
    # transforms
    for(j in 1:nprov) {
      gamma[i, j] <- ilogit(gamma_norm[i, j])
      phi[i, j] <- ilogit(phi_norm[i, j])
      p[i, j] <- ilogit(p_norm[i, j])
    }
  }

  # process model (2 states, p/a)
  # by province
  for(j in 1:nprov) {
    # by individual
    for(i in 1:nindiv) {
      z[i, 1, j] ~ dbern(psi[j])  # initial occupancy probability
      # by year (starting at 2)
      for(t in 2:nyear) {
        muZ[i, t, j] <- z[i, t - 1, j] * phi[t - 1, j] + 
          (1 - z[i, t - 1, j]) * gamma[t - 1, j]
        z[i, t, j] ~ dbern(muZ[i, t, j])
      }
    }
  }

  # observation model
  # by province
  for(j in 1:nprov) {
    # by individual
    for(i in 1:nindiv) {
      # by year
      for(t in 1:nyear) {
        Py[i, t, j] <- z[i, t, j] * p[t, j]
        y[i, t, j] ~ dbern(Py[i, t, j])
      }
    }
  }

  # generate turnover
  for(j in 1:nprov) {
    psivec[1, j] <- psi[j]
    for(t in 2:nyear) {
      psivec[t, j] <- psivec[t - 1, j] * phi[t - 1, j] + 
        (1 - psivec[t - 1, j]) * gamma[t - 1, j]

      turnover[t - 1, j] <- ((1 - psivec[t - 1, j]) * gamma[t - 1, j]) /
        ((1 - psivec[t - 1, j]) * gamma[t - 1, j] + 
        phi[t - 1, j] * psivec[t - 1, j])
    }
  }
}
